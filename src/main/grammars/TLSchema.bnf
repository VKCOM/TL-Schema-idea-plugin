{
  parserClass="com.vk.tlschema.parser.TLSchemaParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="TLSchema"
  psiImplClassSuffix="Impl"
  psiPackage="com.vk.tlschema.psi"
  psiImplPackage="com.vk.tlschema.psi.impl"

  elementTypeHolderClass="com.vk.tlschema.psi.TLSchemaTypes"
  elementTypeClass="com.vk.tlschema.psi.TLSchemaElementType"
  tokenTypeClass="com.vk.tlschema.psi.TLSchemaTokenType"
}


TLSchemaFile ::= constructor_declarations [ ((TRIPPLE_MINUS FUNCTIONS_KW TRIPPLE_MINUS fun_declarations) | (TRIPPLE_MINUS TYPES_KW TRIPPLE_MINUS constructor_declarations)) *]

comment_node ::= COMMENT

uc_ident_node ::= UC_IDENT
lc_ident_node ::= LC_IDENT

attribute_node ::= ATTRIBUTE;

namespace_ident ::= LC_IDENT

lc_ident_ns ::= [ namespace_ident DOT ] lc_ident_node {
    implements=[
        "com.vk.tlschema.psi.TLSchemaNamedElement"
        "com.vk.tlschema.psi.TLSchemaDeclarationBase"
    ]
    mixin="com.vk.tlschema.psi.mixins.TLSchemaLcIdentNsMixin"
}

uc_ident_ns ::= [ namespace_ident DOT ] uc_ident_node {
    implements=[
        "com.vk.tlschema.psi.TLSchemaNamedElement"
        "com.vk.tlschema.psi.TLSchemaDeclarationBase"
    ]
    mixin="com.vk.tlschema.psi.mixins.TLSchemaUcIdentNsMixin"
}

lc_ident_full ::= lc_ident_ns [ SHARP_HEX_NUMBER ] {
    implements="com.vk.tlschema.psi.TLSchemaDeclarationBase"
    mixin="com.vk.tlschema.psi.mixins.TLSchemaLcIdentFullMixin"
}

constructor_declarations ::= [ declaration * ] {
    implements="com.vk.tlschema.psi.TLSchemaNamespacesElement"
    mixin="com.vk.tlschema.psi.mixins.TLSchemaConstructorDeclarationsMixin"
}

fun_declarations ::= [ declaration  * ] {
    implements="com.vk.tlschema.psi.TLSchemaNamespacesElement"
    mixin="com.vk.tlschema.psi.mixins.TLSchemaFunDeclarationsMixin"
}

declaration ::= combinator_decl | combinator_decl_builtin {
    implements="com.vk.tlschema.psi.TLSchemaDeclarationElement"
    mixin="com.vk.tlschema.psi.mixins.TLSchemaDeclarationMixin"
}

combinator_decl_builtin ::= combinator_decl_builtin_type;

combinator_decl_builtin_type ::= full_combinator_id QMARK EQMARK result_type SEMICOLON

type_expr ::= type_term [ type_term * ]
expr ::= term [ term * ]
term ::= BROPEN expr BRCLOSE | type_with_triangle_braces | type_ident | var_ident | nat_expr | percent_term
nat_expr ::= nat_expr_term [ ( PLUS nat_expr_term) *]
nat_expr_term ::= nat_const_term | BROPEN nat_expr BRCLOSE | var_ident
nat_const_term ::= NAT_CONST
type_with_triangle_braces ::= type_ident LEQ type_expr [ (COMA type_expr) * ] GEQ
percent_term ::= PERCENT term
type_ident ::= boxed_type_ident | lc_ident_ns | SHARP
boxed_type_ident ::= uc_ident_ns

var_ident ::= lc_ident_node | uc_ident_node {
    implements=[
        "com.vk.tlschema.psi.TLSchemaNamedElement"
        "com.vk.tlschema.psi.TLSchemaDeclarationBase"
    ]
    mixin="com.vk.tlschema.psi.mixins.TLSchemaVarIdentMixin"
}

type_term ::= term
nat_term ::= nat_expr

combinator_decl ::= [attribute_node *] full_combinator_id [ opt_arg* ] [ arg* ] EQMARK result_type SEMICOLON
full_combinator_id ::= lc_ident_full | UNDERSCORE
opt_arg ::= CRBROPEN var_ident [ var_ident* ] COLON [ EXCMARK ] type_expr CRBRCLOSE
arg ::= arg_array | arg_named | arg_unnamed
arg_array ::= [ var_ident_opt COLON ] [ multiplicity ASTERISK ] SQBROPEN [ arg *] SQBRCLOSE;
arg_named ::= ( var_ident_opt COLON [ conditional_def ] type_term_exc )
arg_unnamed ::=  type_term_exc

type_term_exc ::= type_term | EXCMARK type_term

multiplicity ::= nat_term
var_ident_opt ::= var_ident | UNDERSCORE
conditional_def ::= var_ident [ DOT NAT_CONST ] QMARK

result_type ::= (boxed_type_ident LEQ type_expr [ (COMA type_expr ) * ] GEQ) | (boxed_type_ident [type_expr]) {
    implements="com.vk.tlschema.psi.TLSchemaDeclarationBase"
    mixin="com.vk.tlschema.psi.mixins.TLSchemaResultTypeMixin"
}
